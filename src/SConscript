# -*- Python -*-

import os

Import('env')
env = env.Clone()

# Helper variables
rpath_install = env.Literal(os.path.join('\\$$ORIGIN', '..', 'lib'))
rpath_build = env.Literal(os.path.join('\\$$ORIGIN', '..', 'libs'))
rpath = [rpath_install, rpath_build]
rpath_link = [f'-Wl,-rpath-link={rpath_build}']

# Set compile options.  Normally, this is not done globally, but all bins and shared libs we build will have this set as a starting point for demo/test purposes.
env.AppendUnique(LINKFLAGS = ['-z', 'origin', rpath_link])
env.AppendUnique(RPATH = rpath)
env.AppendUnique(LIBPATH = ['../libs'])

env.SConscript('libs/SConscript', exports='env')
env.SConscript('bins/SConscript', exports='env')
